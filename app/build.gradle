apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.danilov.supermanga"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name


        Properties properties = new Properties()
        properties.load(project.file("local.properties").newDataInputStream())
        resValue "string", "yandex_client_id", properties.getProperty("YCLIENTID")
    }

    retrolambda {
        Properties properties = new Properties()
        def file = project.rootProject.file("config.properties")
        if (file.exists()) {
            properties.load(file.newDataInputStream())

            String jdkPath = properties.getProperty("JDK_PATH")
            if (jdkPath?.trim()) {
                jdk jdkPath
            }
            String oldJDKPath = properties.getProperty("OLD_JDK_PATH")
            if (oldJDKPath?.trim()) {
                oldJdk oldJDKPath
            }
            String jVersion = properties.getProperty("OLD_JDK_VERSION")
            if (jVersion?.trim()) {
                javaVersion JavaVersion.valueOf(jVersion as String)
            }
        }
        defaultMethods false
        incremental true
    }

    lintOptions {
        warning 'ResourceType' //TODO: можно будет убрать с выходом stable Butterknife8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    useLibrary 'org.apache.http.legacy'
}

check {
    pmd {
        config '../quality/pmd-config.xml'
    }
    findbugs {
        skip true
    }
}

android {
    defaultConfig {
        generatedDensities = []
    }

    // This is handled for you by the 2.0+ Gradle Plugin
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
}

dependencies {
    compile libraries.dagger
    apt libraries.daggerCompiler
    compile libraries.javaxInject
    compile libraries.javaxAnnotationApi
    compile libraries.supportAnnotations
    compile libraries.supportV13
    compile libraries.supportAppCompat
    compile libraries.supportDesign
    compile libraries.supportRecyclerView
    compile libraries.supportCardView
    compile libraries.supportPercentView
    compile libraries.streamBackport
    compile libraries.butterKnife
    //yandex disk retarded deps
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okio:okio:1.8.0'
    compile 'org.slf4j:slf4j-api:1.7.10'
    testCompile libraries.junit
    testCompile libraries.robolectric
    testCompile libraries.assertJ
    testCompile libraries.equalsVerifier
    testCompile libraries.okHttpMockWebServer
    testCompile libraries.mockitoCore
    //todo: вынести в deps'ы
    //ЛЕНЬ
    compile 'com.nineoldandroids:library:2.4.0'
    compile files('libs/jsoup-1.7.3.jar')
    compile 'rapid.decoder:library:0.3.0'
    compile 'rapid.decoder:jpeg-decoder:0.3.0'
    compile 'rapid.decoder:png-decoder:0.3.0'
    compile 'com.github.shell-software:fab:1.0.5'
    compile 'de.hdodenhof:circleimageview:1.2.2'
    //google drive api
    compile 'com.google.android.gms:play-services-identity:9.0.0'
    compile 'com.google.android.gms:play-services-base:9.0.0'
    compile 'com.google.android.gms:play-services-drive:9.0.0'
    compile 'com.google.android.gms:play-services-plus:9.0.0'
    compile 'org.mozilla:rhino:1.7R4'
    compile files('libs/acra-4.5.0.jar')
}

apply plugin: 'com.google.gms.google-services'